Alphabet
 А Б В Г Ӷ Ғ Ӻ Д Е Ё Ж З И Й К Ӄ Л М Н Ӈ О П Р ̌ С Т У Ф Х Ӽ Ӿ Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
 а б в г ӷ ғ ӻ д е ё ж з и й к ӄ л м н ӈ о п р с т у ф х ӽ ӿ ц ч ш щ ъ ы ь э ю я ʼ
 %{a%}:а %{a%}:0 %>:0
 %{G%}:г %{G%}:к %{G%}:х %{G%}:ғ
 %{Г%}:г %{Г%}:к %{Г%}:х %{Г%}:ғ
 %{u%}:у %{u%}:0
 %{j%}:й %{j%}:0
 %{T%}:т %{T%}:р
 %{X%}:ӽ %{X%}:ӷ
 %{y%}:ы %{y%}:0
 %{V%}:а %{V%}:э %{V%}:и %{V%}:о %{V%}:у %{V%}:ы
 %{f%}:ф %{f%}:0
 %{I%}:е %{I%}:и
 %{д%}:д %{д%}:т
 %{R%}:р %{R%}:д %{R%}:т
 %{B%}:в %{B%}:б %{B%}:п
 %{Z%}:з %{Z%}:д %{Z%}:т %{Z%}:с
 %{ь%}:ь %{ь%}:0
 %{A%}:а %{A%}:я
 %{ф%}:ф %{ф%}:в
 %{i%}:и %{i%}:0
 %{Ч%}:т %{Ч%}:з %{Ч%}:д %{Ч%}:с
 %{и%}:и %{и%}:й %{и%}:я %{и%}:е %{и%}:ё %{и%}:ю
 %{Q%}:ӄ %{Q%}:ӽ %{Q%}:ӻ
 %{х%}:х %{х%}:ғ
 %{ʼ%}:ʼ %{ʼ%}:̌
 ;

Sets

Vowel = А Е Ё И О У Ы Э Ю Я
        а е ё и о у ы э ю я ;

IotatingVowels = А О У Э
                 а о у э ;

IotatedVowels = Я Ё Ю Е
                я ё ю е ;

Consonant = Б В Г Ӷ Ғ Ӻ Д Ж З Й К Ӄ Л М Н Ӈ П Р Р̌ С Т Ф Х Ӽ Ӿ Ц Ч Ш Щ
            б в г ӷ ғ ӻ д ж з й к ӄ л м н ӈ п р р̌ с т ф х ӽ ӿ ц ч ш щ ;

Sonorant = н ӈ м л ;

! A = Пʼ Тʼ Ч Кʼ Ӄʼ
!     пʼ тʼ ч кʼ ӄʼ ;

A1 = Ф Р С Х Ӽ
     ф р с х ӽ ;

A1a = Ф Р С Ӽ
      ф р с ӽ ;

B = П Т Т К Ӄ
    п т т к ӄ ;

B1 = В Р З Ғ Ӻ
     в р з ғ ӻ ;

C = Б Д Д Г Ӷ
    б д д г ӷ ;

Sibilant = З С
           з с ;

VoicelessC = К Ӄ П Р̌ С Т Ф Х Ӽ Ӿ Ц Ч Ш Щ
             к ӄ п р̌ с т ф х ӽ ӿ ц ч ш щ ;


Rules

"Set ч to с where A goes to A1"
ч:с <=> [:B | :C | :Sonorant (:ь) | :й | :Vowel ] +:0 (:0) _ ;


"Set %{Ч%} to т everywhere except where it becomes з, с, or д"
%{Ч%}:т <=> _ ;
      except
      [ :Vowel | :Sonorant] +:0 (:0) _ ;
      [ :й | :Sonorant ] :B1 _ ;
      :н :ь (%>:) _ ;
      ! :ʼ (%>:) _ ;
      [ :ч | :п ] (:ʼ) (%>:) _ ;

"Change %{Ч%} to д where B goes to C"
%{Ч%}:д <=> [ :Vowel | :Sonorant] +:0 (:0) _ ;

"Change %{Ч%} to з where B goes to B1"
%{Ч%}:з <=> [ :й | :Sonorant ] :B1 _ ;
            :н :ь (%>:) _ ;
            ! :ʼ (%>:) _ ;
      except
            _ :ʼ ;
            :ʼ (%>:) _ ;
            .#. _ ;


! Agentive case
"Remove /a/ after vowel-ending stem"
%{a%}:0 <=> :Vowel (%>:) (:0) _ ;


! Locative and Ablative cases
"Remove /u/ after vowel-ending stem"
%{u%}:0 <=> :Vowel (%>:) (:0) _ ;


! Locative case -- why isn't it working?????
"Remove /j/ after /i/"
%{j%}:0 <=> :и (%>:) %{u%}:0 _ ;


! Comparative case
"Remove /ɤ/ after vowel-ending stem"
%{y%}:0 <=> :Vowel (%>:) (:0) _ ;


! Why?
"Remove /f/ before another stem"
%{f%}:0 <=> _ %>: ;

! Iotating
"Set %{и%} to ю before у"
%{и%}:ю <=> _ (%{f%}:) %>: [ у: | %{u%}: ] ;

"Set %{и%} to е before э"
%{и%}:е <=> _ (%{f%}:) %>: э: ;

"Set %{и%} to ё before о"
%{и%}:ё <=> _ (%{f%}:) %>: о: ;

"Set %{и%} to я before а"
%{и%}:я <=> _ (%{f%}:) %>: [ а: | %{a%}: ] ;

"Set %{и%} to й before ы"
%{и%}:й <=> _ (%{f%}:) %>: :ы ;

"Set %{и%} to 0 before и"
%{и%}:0 <=> _ (%{f%}:) %>: :и ;

!@ %{и%}:ю %{f%}:0 у:0 м:м г:г у:у

"Delete у after ю"
у:0 <=> %{и%}:ю (%{f%}:) %>: _ ;

"Delete я after а"
а:0 <=> %{и%}:я (%{f%}:) %>: _ ;

"Delete е after э"
э:0 <=> %{и%}:е (%{f%}:) %>: _ ;

"Delete ё after о"
о:0 <=> %{и%}:ё (%{f%}:) %>: _ ;


"Remove soft sign archiphoneme in some places"
%{ь%}:0 <=> [ :з | :З ] _ ;
            _ :Vowel ;
            except
              [ :н | :т | :д ] _ :ы ;

"Remove real soft sign before vowels"
ь:0 <=> _ (%>:) :Vowel ;
          except
          _ (%>:) :ы ;

"Iotate vowels after soft sign"
Vx:Vy <=> \:Sibilant [ %{ь%}: | ь: ] (%>:) _ ;
  where Vx in IotatingVowels
    Vy in IotatedVowels
  matched ;

"Voice /f/ before sonorant and vowels"
%{ф%}:в <=> :Vowel (%>:) (:0) _ (%>:) (:0) [:Sonorant | :Vowel] ;

"Voice /x/ before class C"
%{х%}:ғ <=> _ (%>:) :C ;


! For vocative case
"Lengthen preceding vowel"
%{V%}:Vy <=> :Vx (%>:) (:0) _ ;
  where Vx in (а я э е и ы о ё у ю)
    Vy in (а а э э и ы о о у у)
  matched ;

"Add /a/ after consonant at morpheme boundary"
%{V%}:а <=> :Consonant (%>:) (:0) _ ;

! For instrumental case
"Set /G/ to <ғ> after stops and vowels"
%{G%}:ғ <=> [ :B | :Vowel ] (ь:) (%>:) (:0) _ ;

"Set /G/ to <г> after sonorants and in the word нивх"
%{G%}:г <=> [ :Sonorant | %{х%}: ] (%>:) (:0) _ ;

"Set /G/ to <к> after voiceless fricatives"
%{G%}:к <=> :A1a (: ̌) (%>:) (:0) _ ;

! For plurals
"Set /Г/ to <ғ> after voiced stops"
%{Г%}:ғ <=> :C (ь:) %>: _ ;

"Set /Г/ to <г> after sonorants and vowels and in the word нивх"
%{Г%}:г <=> [ :Sonorant | %{х%}: | :Vowel ] %>: _ ;

"Set /Г/ to <к> after voiceless fricatives"
%{Г%}:к <=> :A1a (: ̌) %>: _ ;


! For potential mood
"Set /Q/ to <ӻ> after voiced stops"
%{Q%}:ӻ <=> :C (ь:) %>: _ ;

"Set /Q/ to <ӽ> after voiceless stops"
%{Q%}:ӽ <=> :B (ь:) %>: _ ;


! For dative case
"Set /R/ to <т> after fricatives"
%{R%}:т <=> [ :A1 (: ̌) | :B1 ] (%>:) (:0) _ ;

"Set /R/ to <д> after sonorants"
%{R%}:д <=> :Sonorant (%>:) (:0) _ ;


"Set /B/ to <п> after fricatives"
%{B%}:п <=> [ :A1 (: ̌) | :B1 ] (%>:) _ ;

"Set /B/ to <б> after sonorants"  ! the 'except' includes р when it should include р̌
%{B%}:б <=> [ :Sonorant | :Vowel ] (%>:) _ ;
        except
          _ (:0) :A1  ;

"Change B1 to B"
Cx:Cy <=> :A1 (: ̌) (%>:) _ ;
          where Cx in B1
                Cy in B
          matched ;

"Change B1 to C"
Cx:Cy <=> :Sonorant (:0) (%>:) _ ;
          where Cx in B1
                Cy in C
          matched ;
! Sometimes doesn't change in this context

! Very confused by this rule
"Change B to B1"
Cx:Cy <=> [ :й | :Sonorant ] :B1 _ ;
            :н :ь (%>:) _ ;
      except
            _ :ʼ ;
            :ʼ (%>:) _ ;
            .#. _ ;
      where Cx in B
            Cy in B1
      matched ;

!  "Change B to C for nouns"
  !  Cx:Cy <=> [ :Vowel | :Sonorant ] (:0) %>: %{n%}:0 _ ;
  !        except
  !              _ :ʼ ;
  !        where Cx in B
  !              Cy in C
  !        matched ;

"Change B to C: update"
Cx:Cy <=> [ :Vowel | :Sonorant] +:0 (:0) _ ;
      except
            _ [ :ʼ | ʼ: ] ;
      where Cx in B
            Cy in C
      matched ;

"Change A to A1 after B: update"
Cx:Cy <=> [ :B | :й | :Vowel ] +:0 (:0) _ :ʼ ;
      where Cx in B
            Cy in A1
      matched ;

"Change B to B1 after B: update"
Cx:Cy <=> [ :B ] +:0 (:0) _ ;
      where Cx in B
            Cy in B1
      matched ;

! "Change A to A1 for nouns"
  ! Cx:Cy <=> :Vowel [ :B | :д ] (:0) _ ;
  !      where Cx in B
  !            Cy in A1
  !      matched ;

"Change A1 to B"
Cx:Cy <=> :A1 (: ̌) +:0 (%>:) _ ;
          :Sonorant +:0 (%>:) _ ;
          :B1 +:0 (%>:) _ ;
      where Cx in A1
            Cy in B
      matched ;
! Sometimes doesn't change after sonorants

"Delete <ʼ> after A1"
%{ʼ%}:0 <=> :A1 (%>:) _ ;
        except
            :р _ ;

"Set %{ʼ%} to ̌  after <р>"
%{ʼ%}:̌  <=> :р _ ;

"Keep <ʼ> after B to turn it into correspondent A"
%{ʼ%}:ʼ <=> :B _  ;
        except
            :ч _ ;

"Devoice /z/ after certain aspirates for nouns"
ч:с <=> [ :ч | :п ]  (:ʼ) (%>:) _ ;

"Devoice /z/ after certain aspirates for verbs"
з:с <=> [ :ч | :п ]  (:ʼ) (%>:) _ ;

"Devoice /d/ after voiceless consonants"
%{д%}:т <=> :VoicelessC %>: _ ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst
