Multichar_Symbols

! Part of speech categories
%<n%>   ! Noun
%<v%>   ! Verb
%<adv%> ! Adverb
%<cnjadv%> ! Conjunctive adverb
%<prn%> ! Pronoun
%<num%> ! Numeral
!%<enc%> ! Enclitic
%<det%> ! Determiner

! Case categories
%<nom%> ! Nominative
%<age%> ! Agentive
%<dat%> ! Dative
%<ins%> ! Instrumental
%<loc%> ! Locative
%<abl%> ! Ablative
%<term%> ! Terminative
%<comp%> ! Comparative
%<voc%> ! Vocative

! Pronoun categories
%<pers%> ! Personal
%<dem%> ! Demonstrative
%<indef%> ! Indefinite
%<itg%> ! Interrogative
%<ref%> ! Reflexive

! Person categories
%<p1%> ! First person
%<p2%> ! Second person
%<p3%> ! Third person

! Number categories
%<sg%> ! Singular
%<du%> ! Dual
%<pl%> ! Plural

! Clusivity categories
%<incl%> ! Inclusive
%<excl%> ! Exclusive

! Verb categories
%<tv%> ! Transitive verb
%<iv%> ! Intransitive verb

! Verb forms
%<inf%> ! infinitive
%<cvb_nar1%> ! narrative converb (null suffix)
%<cvb_nar2%> ! narrative converb (-data/-durngu)
%<cvb_ant%> ! anterior/simultaneous converb
%<cvb_cond%> ! conditional converb ("if")
%<nmz_pred%> ! Predicate-actant nominalisation
%<nmz_subj%> ! Subject nominalisation
%<nmz_loc%> ! Locative nominalisation
%<nmz_adv%> ! Adverbial nominalisation

! Mood categories (for now)
%<ind%> ! Indicative
! %<imp%> ! Imperative
%<itg%> ! Interrogative

! Evidentiality / stuff
! may matter later

! Tense categories
%<fut%> ! Future

! Noun categories
%<np%> Proper noun

! Numeral classes
%<cl1%>
%<cl2%>
%<cl3%>
%<cl4%>
%<cl5%>
%<cl6%>
%<cl7%>
%<cl8%>
%<cl9%>

! Formality categories
%<frm%> ! Formal
%<infrm%> ! Informal

! Noun Attribute
%<attr%> ! Noun attribute

! Other symbols
! %<redup%> ! Reduplication
%<qst%>
%<foc%>
%<emph%>
%<poss%>
%>      ! Morpheme boundary
%{Х%}     ! terminative (Cyrillic)
%{I%}     ! in some numeral
!%ʼ
%{ʼ%}
%{и%}

%{д%}     ! undergo voicing
%{ф%}

%{a%}     ! delete sometimes
%{o%}
%{u%}
%{y%}
%{j%}
%{f%}
%{ь%}

%{T%}     ! (Latin)
%{X%}     ! (Latin)
%{V%}
%{R%}
%{G%}
%{v%}
%{n%}

! Punctuation tags
%<sent%>      ! Sentence marker
%<guio%>      ! Hyphen
%<cm%>        ! Comma
%<apos%>      ! Apostrophe
%<rquot%>     ! Quote marker (right hand side)
%<lquot%>     ! Quote marker (left hand side)
%<rpar%>      ! Parenthetical marker (right hand side)
%<lpar%>      ! Parenthetical marker (left hand side)


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounRoot ;
ProperNounRoot ;
PronounRoot ;
DeterminerRoot ;
NumeralRoot ;
VerbRoot ;
AdverbRoot ;
Punctuation ;

LEXICON CLITICS

%<qst%>:%>ла # ; ! Y/N questions
%<emph%>:%>%{R%}и # ; ! intensifying particle 'even'
%<emph%>:%>ӿагин # ; ! intensifying particle 'even'
%<foc%>:%>ра # ; ! focus particle
%<foc%>:%>ат # ; ! focus particle

# ;

LEXICON CASES
%<age%>:%>%{a%}ӽ CLITICS ;             ! Agentive
%<dat%>:%>%{R%}%{o%}ӽ CLITICS ;       ! Dative
%<ins%>:%>%{G%}ир CLITICS ;           ! Instrumental
%<loc%>:%>%{u%}%{j%}н CLITICS ;       ! Locative
%<abl%>:%>%{u%}х CLITICS ;            ! Ablative
%<term%>:%>%{T%}%{ʼ%}ыкы CLITICS ;    ! Terminative
%<term%>:%>%{T%}%{ʼ%}%{X%}а CLITICS ; ! Dir/LR
%<term%>:%>%{R%}оӻо CLITICS ;         ! Dir/LR
%<comp%>:%>%{y%}к CLITICS ;           ! Comparative
%<voc%>:%>%{V%} # ;                   ! Vocative
INCORPORATE-N ;                        ! Nominative

LEXICON N-INFL

CASES ;

LEXICON SUBST

N-INFL;
%<pl%>:%>%{G%}у N-INFL ;

LEXICON N1

%<n%>: # ; ! attributive noun
%<n%>: SUBST ;

LEXICON PN1
%<n%>%<np%>: N-INFL ;

LEXICON POSS

%<poss%>:%>ны CASES ;
# ;

LEXICON PRN1

%<prn%>%<pers%>%<p1%>%<sg%>:и POSS ;
%<prn%>%<pers%>%<p1%>%<sg%>%+:ь%> TVerbRoot ;
%<prn%>%<pers%>%<p1%>%<sg%>%+:ь%> NounRoot ;

LEXICON PRN2

%<prn%>%<pers%>%<p2%>%<sg%>:и POSS ;
%<prn%>%<pers%>%<p2%>%<sg%>%+:ʼ%> TVerbRoot ;
%<prn%>%<pers%>%<p2%>%<sg%>%+:ʼ%> NounRoot ;

LEXICON PRN3

%<prn%>%<pers%>%<p3%>%<sg%>: POSS ;
%<prn%>%<pers%>%<p3%>%<sg%>%+:%> TVerbRoot ;
%<prn%>%<pers%>%<p3%>%<sg%>%+:%> NounRoot ;

LEXICON PRN4

%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>: POSS ;
%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>%+:%> TVerbRoot ;
%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>%+:%> NounRoot ;
%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>:%> CASES ;

LEXICON PRN5

%<prn%>%<pers%>%<p1%>%<pl%>%<incl%>: POSS ;
%<prn%>%<pers%>%<p1%>%<pl%>%<incl%>: # ;
%<prn%>%<pers%>%<p1%>%<pl%>%<incl%>:%> CASES ;

LEXICON PRN6

%<prn%>%<pers%>%<p1%>%<du%>%<incl%>: POSS ;
%<prn%>%<pers%>%<p1%>%<du%>%<incl%>: # ;
%<prn%>%<pers%>%<p1%>%<du%>%<incl%>:%> CASES ;

LEXICON PRN7

%<prn%>%<pers%>%<p2%>%<pl%>: POSS ;
%<prn%>%<pers%>%<p2%>%<pl%>%+:%> TVerbRoot ;
%<prn%>%<pers%>%<p2%>%<pl%>%+:%> NounRoot ;
%<prn%>%<pers%>%<p2%>%<pl%>:%> CASES ;

LEXICON PRN8

%<prn%>%<pers%>%<p3%>%<pl%>: POSS ;
%<prn%>%<pers%>%<p3%>%<pl%>%+:%> TVerbRoot ;
%<prn%>%<pers%>%<p3%>%<pl%>%+:%> NounRoot ;
%<prn%>%<pers%>%<p3%>%<pl%>:%> CASES ;

LEXICON PRN9

%<prn%>%<ref%>:и POSS ;
%<prn%>%<ref%>%+:%> TVerbRoot ;
%<prn%>%<ref%>%<poss%>%+:%> NounRoot ;

LEXICON IndefinitePRN

%<prn%>%<indef%>: # ;
%<prn%>%<indef%>%+:%> TVerbRoot ; ! Not sure if it can decline to other cases

LEXICON DemonstrativePRN

%<prn%>%<dem%>: # ;
%<prn%>%<dem%>%+:%> TVerbRoot ;

LEXICON InterrogativePRN
%<prn%>%<itg%>: # ;

LEXICON DET
%<det%>%<dem%>%+:%> NounRoot ;

LEXICON SubjAgree

%<p1%>%<sg%>:%>т CLITICS ;
%<p2%>%<sg%>:%>р CLITICS ;
%<p3%>%<sg%>:%>р CLITICS ;
%<p1%>%<pl%>:%>т CLITICS ;
%<p2%>%<pl%>:%>т CLITICS ;
%<p3%>%<pl%>:%>т CLITICS ;

LEXICON ADV1

%<adv%>:%>%{G%}у SubjAgree ;

LEXICON COMP

%<comp%>:%>ё ADV1;
ADV1 ;

LEXICON NUM1

%<num%>%<cl1%>:%>%{I%}н # ;
%<num%>%<cl2%>:%>ынь # ;
%<num%>%<cl3%>:%>аӄр # ;

LEXICON NUM2

%<num%>%<cl1%>:%>ен # ;
%<num%>%<cl2%>:%>ор # ;
%<num%>%<cl3%>:%>еӄр # ;
%<num%>%<cl4%>:%>евсӄ # ;
%<num%>%<cl5%>:%>им # ;
%<num%>%<cl6%>:%>ер # ;
%<num%>%<cl7%>:%>евор # ;
%<num%>%<cl8%>:%>ерах # ;
%<num%>%<cl9%>:%>ик # ;

LEXICON FORMALITY
%<frm%>:о CLITICS ;
%<infrm%>: CLITICS ;

LEXICON MOOD

%<itg%>:%>л FORMALITY ;
%<ind%>:%>%{д%}ь CLITICS ;
! %<imp%>:%>idk # ;
# ;

INCORPORATE-V ;

LEXICON NonFinite

%<inf%>:%>%{R%}оӽ CLITICS ;
%<cvb_nar1%>: SubjAgree ;
%<cvb_nar2%>:%>%{д%}ата SubjAgree ;   ! Dir/LR
%<cvb_nar2%>:%>%{д%}урӈу SubjAgree ;
%<cvb_ant%>:%>ӈан CLITICS ;
%<cvb_cond%>:%>ӻа CLITICS ;
MOOD ;

LEXICON TENSE

%<fut%>:%>ны NonFinite ;
NonFinite ;

LEXICON TV1
%<v%>%<tv%>: TENSE;

LEXICON IV1
%<v%>%<iv%>: TENSE;

LEXICON INCORPORATE-N
%+:%>%{v%} TVerbRoot ;
%+:%>%{v%} IVerbRoot ;
%+:%>%{v%} IrregBoundVerbRoot ;
CLITICS ;

LEXICON INCORPORATE-V
%+:%>%{n%} NounRoot ;

LEXICON CnjADV
%<cnjadv%>: CLITICS;

LEXICON ADV2
%<adv%>: CLITICS;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot
умгу:умгу N1          ; ! woman
ытык:ытык N1          ; ! father
ӄан:ӄан N1            ; ! doggo
ӈа:ӈа N1              ; ! animal
тыф:ты%{ф%} N1        ; ! house
ми:ми N1              ; ! inside
нивх:нивх N1          ; ! person, man
ваӄа:ваӄа N1          ; ! box
сета:сета N1          ; ! sugar
ойдам:ойдам N1        ; ! baby
курӈ:курӈ N1          ; ! universe
тяӄо:ч%{ь%}аӄо N1     ; ! knife
оола:оола N1          ; ! child
чʼатьф:чʼ{ь%}ать%{ф%} N1 ; ! bog, swamp
ыф:ы%{ф%} N1          ; ! beard
арф:ар%{ф%} N1        ; ! side of boat
ӿуньф:ӿунь%{ф%} N1    ; ! barrel
пердь:пердь N1        ; ! burden
ыкын:ыкын N1          ; ! older brother
атик:атик N1          ; ! younger brother
нанак:нанак N1        ; ! older sister
ытик:ытик N1          ; ! grandmother
пос:пос N1            ; ! fabric, cloth
ӈафӄ:ӈафӄ N1          ; ! friend
леп:леп N1            ; ! bread
ларӄ:ларӄ N1          ; ! shirt
кинс:кинс N1          ; ! devil
по:по N1              ; ! village
кʼеӄ:к%{ʼ%}еӄ N1      ; ! fox
чеврӄ:ч{ь%}еврӄ N1    ; ! bird
тупр̌:тупр̌ N1          ; ! feather
тюс:тюс N1            ; ! meat
мулк:мулк N1          ; ! basket
ӈояӄ:ӈояӄ N1          ; ! egg
ӄʼас:ӄ%{ʼ%}ас N1      ; ! tambourine
выйбукс:выйбукс N1    ; ! tie
мифпʼидь:мифпʼидь N1  ; ! toad
ӄʼос:ӄ%{ʼ%}ос N1      ; ! neck
олғоӈ:олғоӈ N1        ; ! pig
коӽ:коӽ N1            ; ! belly
пыньх:пыньх N1        ; ! soup
нё:нё N1              ; ! barn
ӿеӄрӈа:ӿеӄрӈа N1      ; ! place further from shore (than here)
лумр:лумр N1          ; ! sable
ӿонтӄ:ӿонтӄ N1        ; ! sack
ӿемар:ӿемар N1        ; ! old man
паӽ:паӽ N1            ; ! stone
толф:тол%{ф%} N1      ; ! summer
ма:ма N1              ; ! dried fish
питғы:питғы N1        ; ! paper (written), book
пон:пон N1            ; ! villager
кʼеӈ:к%{ʼ%}еӈ N1      ; ! sun
!!! New words !!!
ӈыньк:ӈыньк N1	      ; ! face
ӈайн:ӈайн N1	      ; ! cheek
т’ыр:т%{’%}ыр N1      ; ! left (not right)
ӈир:ӈир N1	      ; ! right (not left)
ӈамадь:ӈамадь N1      ; ! skin
няӽ:няӽ N1	      ; ! eye
них:них N1	      ; ! tear (from the eye)
т’адм:т’адм N1	      ; ! pate, crown of head
т’а:т’а N1	      ; ! pate, crown of head
ӿеғми:ӿеғми N1	      ; ! temple (of head)
нос:нос N1	      ; ! ear
мла:мла N1	      ; ! earlobe
вих:вих N1	      ; ! nose
ньхыр:ньхыр N1	      ; ! eyebrow
ыӈг:ыӈг N1	      ; ! mouth
ӈығс:ӈығс N1	      ; ! tooth
эмс:эмс N1	      ; ! molar
ӈыньф:ӈыньф N1	      ; ! bone
ывлх:ывлх N1	      ; ! lip
ити:ити N1	      ; ! chin
оӻри:оӻри N1	      ; ! nape, occiput

LEXICON ProperNounRoot
Рум:Рум PN1      ; ! dude called Rum

LEXICON PronounRoot
PersPronRoot ;
IndefPronRoot ;
DemPronRoot ;
IntPronRoot ;

LEXICON PersPronRoot
ни:н PRN1        ; ! I
чи:ч PRN2        ; ! you
иф:%{и%}%{f%} PRN3 ; ! he, she, it
ньыӈ:ньыӈ PRN4   ; ! they and I
мер:мер PRN5     ; ! y'all and I
меги:меги PRN6   ; ! you and I
чыӈ:чыӈ PRN7     ; ! y'all
имӈ:имӈ PRN8     ; ! they
пʼи:пʼ PRN9      ; ! oneself

LEXICON IndefPronRoot
ӿуӈрыдь:ӿуӈрыдь IndefinitePRN ; ! something like this

LEXICON DemPronRoot
тыдь:тыдь DemonstrativePRN ; ! this (one)
ӿыдь:ӿыдь DemonstrativePRN ; ! that (one)
адь:адь DemonstrativePRN   ; ! that (one)

LEXICON IntPronRoot
аӈ:аӈ InterrogativePRN ; ! who

LEXICON DeterminerRoot
ӿы:ӿы DET ; ! this

LEXICON NumeralRoot

ньин:нь NUM1 ; ! one
мен:м NUM2   ; ! two

LEXICON VerbRoot
TVerbRoot ;
IVerbRoot ;
IVerb-AdvRoot ;
IrregFreeVerbRoot ;

LEXICON TVerbRoot
за:з{ь%}а TV1        ; ! to beat, strike
во:во TV1            ; ! to take
даю:даю TV1          ; ! to write
ғе:ғе TV1            ; ! to buy, take
ӄала:ӄала TV1        ; ! to hate
ама:ама TV1          ; ! to look at
иғ:иғ TV1            ; ! to kill
ху:х%{ʼ%}у TV1       ; ! to kill
ӽа:ӽ%{ʼ%}а TV1       ; ! to shoot
фыврк:ф%{ʼ%}ыврк TV1 ; ! to pluck
р̌а:р̌%{ʼ%}а TV1       ; ! to fry
фын:ф%{ʼ%}ын TV1     ; ! to throw
хир:х%{ʼ%}ир TV1     ; ! to use
ыӈг:ыӈг TV1          ; ! to rip up
мот:мот TV1          ; ! to kiss
ар:ар TV1            ; ! to feed
ӽат:ӽ%{ʼ%}ат TV1     ; ! to scold
амӽта:амӽта TV1      ; ! to praise
ыз:ыз TV1            ; ! to call
ор:ор TV1            ; ! to meet
ӈарма:ӈарма TV1      ; ! to wait for, await
ӽони:ӽ%{ʼ%}они TV1   ; ! to save, rescue
ньр̌ы:ньр̌ы TV1        ; ! to see
р̌ык:р̌%{ʼ%}ык TV1     ; ! to bring home
имы:имы TV1          ; ! to give
лызи:лызи TV1        ; ! to pass, go past
рғоп:рғоп TV1        ; ! to touch
р̌ыпр:р̌%{ʼ%}ыпр TV1   ; ! to bring
эзму:эзму TV1        ; ! to love


LEXICON IVerbRoot
пʼры:пʼры IV1     ; ! to come
му:му IV1         ; ! to die
чека:чека IV1     ; ! to be strong
ӿарӻо:ӿарӻо IV1   ; ! to be strong
чез:чез IV1       ; ! to be weak
чола:чола IV1     ; ! to be poor
ӈалы:ӈалы IV1     ; ! to be delirious, to talk nonsense
ӄʼо:ӄʼо IV1       ; ! to sleep
ви:ви IV1         ; ! to go
пʼу:пʼу IV1       ; ! to go out, exit
тывғ:тывғ IV1     ; ! to enter a house
тʼаӽта:тʼаӽта IV1 ; ! to be angry
ӿеӻа:ӿеӻа IV1     ; ! to be afraid
пʼолағ:пʼолағ IV1 ; ! to shout
пʼим:пʼим IV1     ; ! to run away
ӄотю:ӄотю IV1     ; ! to fall silent
оз:оз IV1         ; ! to stand up
кут:кут IV1       ; ! to fall
лук:лук IV1       ; ! to be fluffy
там:там IV1       ; ! to be numerous
маӈӻ:маӈӻ IV1     ; ! to be powerful, to be rich
эзму:эзму IV1     ; ! to rejoice

LEXICON IrregBoundVerbRoot
ни:ни TV1         ; ! to eat

LEXICON IrregFreeVerbRoot
инь:инь TV1       ; ! to eat

LEXICON IVerb-AdvRoot
ур:ур COMP ; ! well
еӻ:еӻ COMP ; ! quickly

LEXICON AdverbRoot
CnjAdvRoot ;
AdvRoot ;

LEXICON CnjAdvRoot
ӿоӻор:ӿоӻор CnjADV        ; ! then, thereupon
ӿоӻорӈан:ӿоӻорӈан CnjADV  ; ! then, at that time
ӿаӈан:ӿаӈан CnjADV        ; ! then, in that case

LEXICON AdvRoot
напа:напа ADV2     ; ! still
нав:нав ADV2       ; ! now
лили:лили ADV2     ; ! very
тʼыфк:тʼыфк ADV2   ; ! long ago, a long time ago
нымр:нымр ADV2     ; ! yesterday

LEXICON Punctuation
.%<sent%>:. # ;
..%<sent%>:.. # ;
...%<sent%>:... # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
\%<sent%>:\ # ;
/%<sent%>:/ # ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst
