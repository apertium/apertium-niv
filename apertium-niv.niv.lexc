Multichar_Symbols

! Part of speech categories
%<n%>   ! Noun
%<v%>   ! Verb
%<adv%> ! Adverb
%<prn%> ! Pronoun
%<num%> ! Numeral
!%<enc%> ! Enclitic

! Case categories
%<nom%> ! Nominative
%<age%> ! Agentive
%<dat%> ! Dative
%<ins%> ! Instrumental
%<loc%> ! Locative
%<abl%> ! Ablative
%<term%> ! Terminative
%<comp%> ! Comparative
%<voc%> ! Vocative

! Pronoun categories
%<pers%> ! Personal
%<dem%> ! Demonstrative
%<ind%> ! Indefinite
%<itg%> ! Interrogative
%<ref%> ! Reflexive

! Person categories
%<p1%> ! First person
%<p2%> ! Second person
%<p3%> ! Third person

! Number categories
%<sg%> ! Singular
%<du%> ! Dual
%<pl%> ! Plural

! Clusivity categories
%<incl%> ! Inclusive
%<excl%> ! Exclusive

! Verb categories
%<tv%> ! Transitive verb
%<iv%> ! Intransitive verb

! Verb forms
%<inf%> ! infinitive
%<cvb_nar%> ! narrative converb
%<cvb_ant%> ! anterior/simultaneous converb
%<nmz.pred%> ! Predicate-actant nominalisation
%<nmz.subj%> ! Subject nominalisation
%<nmz.loc%> ! Locative nominalisation
%<nmz.adv%> ! Adverbial nominalisation

! Mood categories (for now)
%<ind%> ! Indicative
! %<imp%> ! Imperative
! %<itg%> ! Interrogative

! Evidentiality / stuff
! may matter later

! Tense categories
%<fut%> ! Future

! Noun categories
%<np%> Proper noun

! Numeral classes
%<cl1%>
%<cl2%>
%<cl3%>
%<cl4%>
%<cl5%>
%<cl6%>
%<cl7%>
%<cl8%>
%<cl9%>

! Formality categories
%<frm%> ! Formal
%<infrm%> ! Informal

! Noun Attribute
%<attr%> ! Noun attribute

! Other symbols
! %<redup%> ! Reduplication
%>      ! Morpheme boundary
%{a%}
%{G%}
%{R%}
%{o%}
%{u%}
%{j%}
%{T%}
%{X%}
%{y%}
%{V%}
%{f%}
%{I%}
%{Д%}
%{Z%}
%{F%}
%ʼ
%{ь%}

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounRoot ;
ProperNounRoot ;
PronounRoot ;
NumeralRoot ;
VerbRoot ;

LEXICON CLITICS

%<qst%>:%>ла # ; ! Y/N questions
%<emph%>:%>%{R%}и # ; ! intensifying particle 'even'
%<emph%>:%>ӿагин # ; ! intensifying particle 'even'
0:0 # ;

LEXICON CASES
%<age%>:%>%{a%}ӽ CLITICS ;       ! Agentive
%<dat%>:%>%{R%}%{o%}ӽ CLITICS ;  ! Dative
%<ins%>:%>%{G%}ир CLITICS ;      ! Instrumental
%<loc%>:%>%{u%}%{j%}н CLITICS ;  ! Locative
%<abl%>:%>%{u%}х CLITICS ;       ! Ablative
%<term%>:%>%{T%}ыкы CLITICS ;    ! Terminative
%<term%>:%>%{T%}%{X%}а CLITICS ; ! Dir/LR
%<term%>:%>%{R%}оӻо CLITICS ;    ! Dir/LR
%<comp%>:%>%{y%}к CLITICS ;      ! Comparative
%<voc%>:%>%{V%} # ;              ! Vocative
CLITICS ;                        ! Nominative

LEXICON N-INFL

CASES;

LEXICON SUBST

N-INFL;
%<pl%>:%>%{G%}у N-INFL ;

LEXICON N1

!%<n%>: # ; ! attributive noun
%<n%>: SUBST ;

LEXICON PN1
%<n%>%<np%>: N-INFL ;

LEXICON POSS

%<pos%>:%>ны CASES ;
0:0 # ;

LEXICON PRN1

%<prn%>%<pers%>%<p1%>%<sg%>:и POSS ;
%<prn%>%<pers%>%<p1%>%<sg%>%+:ь%> TVerbRoot ;
%<prn%>%<pers%>%<p1%>%<sg%>%+:ь%> NounRoot ;

LEXICON PRN2

%<prn%>%<pers%>%<p2%>%<sg%>:и POSS ;
%<prn%>%<pers%>%<p2%>%<sg%>%+:ʼ%> TVerbRoot ;
%<prn%>%<pers%>%<p2%>%<sg%>%+:ʼ%> NounRoot ;

LEXICON PRN3

%<prn%>%<pers%>%<p3%>%<sg%>: POSS ;
%<prn%>%<pers%>%<p3%>%<sg%>%+:%> NounRoot ;

LEXICON PRN4

%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>: POSS ;
%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>%+:%> TVerbRoot ;
%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>%+:%> NounRoot ;

LEXICON PRN5

%<prn%>%<pers%>%<p1%>%<pl%>%<incl%>: POSS ;
%<prn%>%<pers%>%<p1%>%<pl%>%<incl%>: # ;

LEXICON PRN6

%<prn%>%<pers%>%<p1%>%<du%>%<incl%>: POSS ;
%<prn%>%<pers%>%<p1%>%<du%>%<incl%>: # ;

LEXICON PRN7

%<prn%>%<pers%>%<p2%>%<pl%>: POSS ;
%<prn%>%<pers%>%<p2%>%<pl%>%+:%> TVerbRoot ;
%<prn%>%<pers%>%<p2%>%<pl%>%+:%> NounRoot ;

LEXICON PRN8

%<prn%>%<pers%>%<p3%>%<pl%>: POSS ;
%<prn%>%<pers%>%<p3%>%<pl%>%+:%> TVerbRoot ;
%<prn%>%<pers%>%<p3%>%<pl%>%+:%> NounRoot ;

LEXICON PRN9

%<prn%>%<ref%>: POSS ;
%<prn%>%<ref%>%+:%> TVerbRoot ;
%<prn%>%<ref%>%+:%> NounRoot ;

LEXICON SubjAgree

%<p1%>%<sg%>:%>т # ;
%<p2%>%<sg%>:%>р # ;
%<p3%>%<sg%>:%>р # ;
%<p1%>%<pl%>:%>т # ;
%<p2%>%<pl%>:%>т # ;
%<p3%>%<pl%>:%>т # ;

LEXICON ADV1

%<adv%>:%>%{G%}у SubjAgree ;

LEXICON COMP

%<comp%>:%>ё ADV1;
ADV1 ;

LEXICON NUM1

%<num%>%<cl1%>:%>%{I%}н # ;
%<num%>%<cl2%>:%>ынь # ;
%<num%>%<cl3%>:%>аӄр # ;

LEXICON NUM2

%<num%>%<cl1%>:%>ен # ;
%<num%>%<cl2%>:%>ор # ;
%<num%>%<cl3%>:%>еӄр # ;
%<num%>%<cl4%>:%>евсӄ # ;
%<num%>%<cl5%>:%>им # ;
%<num%>%<cl6%>:%>ер # ;
%<num%>%<cl7%>:%>евор # ;
%<num%>%<cl8%>:%>ерах # ;
%<num%>%<cl9%>:%>ик # ;

LEXICON FORMALITY
%<frm%>:%>о # ;
%<infrm%>:0 # ;

LEXICON MOOD

%<itg%>:%>л FORMALITY ;
%<ind%>:%>%{Д%}ь # ;
! %<imp%>:%>idk # ;

LEXICON NonFinite

%<inf%>:%>%{R%}оӽ # ;
%<cvb_nar%>: SubjAgree ;
%<cvb_ant%>:%>ӈан # ;
MOOD ;

LEXICON TENSE

%<fut%>:%>ны NonFinite ;
NonFinite ;

LEXICON TV1

%<v%>%<tv%>: TENSE;

LEXICON IV1

%<v%>%<iv%>: TENSE;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot

умгу:умгу N1  ; ! woman
ытык:ытык N1  ; ! father
ӄан:ӄан N1    ; ! doggo
ӈа:ӈа N1      ; ! animal
тыф:%{R%}ы{%F%} N1    ; ! house
ми:ми N1         ; ! inside
нивх:нивх N1     ; ! person
ваӄа:ваӄа N1     ; ! box
сета:сета N1     ; ! sugar
ойдам:ойдам N1   ; ! baby
курӈ:курӈ N1     ; ! universe
тяӄо:%{Z%}%{ь%}аӄо N1 ; ! knife
оола:оола N1     ; ! child

LEXICON ProperNounRoot
Рум:Рум PN1      ; ! dude called Rum


LEXICON PronounRoot

ни:н PRN1        ; ! I
чи:ч PRN2        ; ! you
иф:и%{f%} PRN3   ; ! he, she, it
ньыӈ:ньыӈ PRN4   ; ! they and I
мер:мер PRN5     ; ! y'all and I
меги:меги PRN6   ; ! you and I
чыӈ:чыӈ PRN7     ; ! y'all
имӈ:имӈ PRN8     ; ! they
пʼи:пʼ PRN9      ; ! oneself

LEXICON NumeralRoot

ньин:нь NUM1 ; ! one
мен:м NUM2   ; ! two

LEXICON VerbRoot
:%{i%}%> TVerbRoot ;
IVerbRoot ;
IVerb-AdvRoot ;

LEXICON TVerbRoot

за:%{Z%}а TV1 ; ! to beat
во:%{i%}%{P%}о TV1 ; ! to take
даю:даю TV1 ; ! to write
ғе:ғе TV1 ; ! to buy
ӄала:ӄала ; ! to hate

LEXICON IVerbRoot

пʼры:пʼры IV1 ; ! to come
му:му IV1     ; ! to die
чека:чека IV1 ; ! to be strong
чола:чола IV1 ; ! to be poor

LEXICON IVerb-AdvRoot
ур:ур COMP ; ! well
еӻ:еӻ COMP ; ! quickly

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst
