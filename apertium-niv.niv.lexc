Multichar_Symbols

! Part of speech categories
%<n%>   ! Noun
%<v%>   ! Verb
%<adv%> ! Adverb
%<cnjadv%> ! Conjunctive adverb
%<prn%> ! Pronoun
%<num%> ! Numeral
!%<enc%> ! Enclitic

! Case categories
%<nom%> ! Nominative
%<age%> ! Agentive
%<dat%> ! Dative
%<ins%> ! Instrumental
%<loc%> ! Locative
%<abl%> ! Ablative
%<term%> ! Terminative
%<comp%> ! Comparative
%<voc%> ! Vocative

! Pronoun categories
%<pers%> ! Personal
%<dem%> ! Demonstrative
%<indef%> ! Indefinite
%<itg%> ! Interrogative
%<ref%> ! Reflexive

! Person categories
%<p1%> ! First person
%<p2%> ! Second person
%<p3%> ! Third person

! Number categories
%<sg%> ! Singular
%<du%> ! Dual
%<pl%> ! Plural

! Clusivity categories
%<incl%> ! Inclusive
%<excl%> ! Exclusive

! Verb categories
%<tv%> ! Transitive verb
%<iv%> ! Intransitive verb

! Verb forms
%<inf%> ! infinitive
%<cvb_nar%> ! narrative converb
%<cvb_ant%> ! anterior/simultaneous converb
%<cvb_cond%> ! conditional converb ("if")
%<nmz_pred%> ! Predicate-actant nominalisation
%<nmz_subj%> ! Subject nominalisation
%<nmz_loc%> ! Locative nominalisation
%<nmz_adv%> ! Adverbial nominalisation

! Mood categories (for now)
%<ind%> ! Indicative
! %<imp%> ! Imperative
%<itg%> ! Interrogative

! Evidentiality / stuff
! may matter later

! Tense categories
%<fut%> ! Future

! Noun categories
%<np%> Proper noun

! Numeral classes
%<cl1%>
%<cl2%>
%<cl3%>
%<cl4%>
%<cl5%>
%<cl6%>
%<cl7%>
%<cl8%>
%<cl9%>

! Formality categories
%<frm%> ! Formal
%<infrm%> ! Informal

! Noun Attribute
%<attr%> ! Noun attribute

! Other symbols
! %<redup%> ! Reduplication
%<qst%>
%<foc%>
%<emph%>
%<poss%>
%>      ! Morpheme boundary
%{Х%}     ! terminative (Cyrillic)
%{I%}     ! in some numeral
!%ʼ
%{ʼ%}

%{д%}     ! undergo voicing
%{ф%}

%{a%}     ! delete sometimes
%{o%}
%{u%}
%{y%}
%{j%}
%{f%}
%{ь%}

%{P%}     ! aspirated nouns
%{T%}     ! (Latin)
%{T2%}    ! (Latin)
%{C%}     ! (Latin)
%{K%}     ! (Latin)
%{Q%}

%{X%}     ! (Latin)
%{V%}
%{R%}
%{G%}
%{v%}
%{n%}

! Punctuation tags
%<sent%>      ! Sentence marker
%<guio%>      ! Hyphen
%<cm%>        ! Comma
%<apos%>      ! Apostrophe
%<rquot%>     ! Quote marker (right hand side)
%<lquot%>     ! Quote marker (left hand side)
%<rpar%>      ! Parenthetical marker (right hand side)
%<lpar%>      ! Parenthetical marker (left hand side)


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounRoot ;
ProperNounRoot ;
PronounRoot ;
DeterminerRoot ;
NumeralRoot ;
VerbRoot ;
AdverbRoot ;
Punctuation ;

LEXICON CLITICS

%<qst%>:%>ла # ; ! Y/N questions
%<emph%>:%>%{R%}и # ; ! intensifying particle 'even'
%<emph%>:%>ӿагин # ; ! intensifying particle 'even'
%<foc%>:%>ра # ; ! focus particle
%<foc%>:%>ат # ; ! focus particle

# ;

LEXICON CASES
%<age%>:%>%{a%}ӽ CLITICS ;             ! Agentive
%<dat%>:%>%{R%}%{o%}ӽ CLITICS ;       ! Dative
%<ins%>:%>%{G%}ир CLITICS ;           ! Instrumental
%<loc%>:%>%{u%}%{j%}н CLITICS ;       ! Locative
%<abl%>:%>%{u%}х CLITICS ;            ! Ablative
%<term%>:%>%{T%}%{ʼ%}ыкы CLITICS ;    ! Terminative
%<term%>:%>%{T%}%{ʼ%}%{X%}а CLITICS ; ! Dir/LR
%<term%>:%>%{R%}оӻо CLITICS ;         ! Dir/LR
%<comp%>:%>%{y%}к CLITICS ;           ! Comparative
%<voc%>:%>%{V%} # ;                   ! Vocative
INCORPORATE-N ;                        ! Nominative

LEXICON N-INFL

CASES ;

LEXICON SUBST

N-INFL;
%<pl%>:%>%{G%}у N-INFL ;

LEXICON N1

%<n%>: # ; ! attributive noun
%<n%>: SUBST ;

LEXICON PN1
%<n%>%<np%>: N-INFL ;

LEXICON POSS

%<poss%>:%>ны CASES ;
# ;

LEXICON PRN1

%<prn%>%<pers%>%<p1%>%<sg%>:и POSS ;
%<prn%>%<pers%>%<p1%>%<sg%>%+:ь%> TVerbRoot ;
%<prn%>%<pers%>%<p1%>%<sg%>%+:ь%> NounRoot ;

LEXICON PRN2

%<prn%>%<pers%>%<p2%>%<sg%>:и POSS ;
%<prn%>%<pers%>%<p2%>%<sg%>%+:ʼ%> TVerbRoot ;
%<prn%>%<pers%>%<p2%>%<sg%>%+:ʼ%> NounRoot ;

LEXICON PRN3

%<prn%>%<pers%>%<p3%>%<sg%>: POSS ;
%<prn%>%<pers%>%<p3%>%<sg%>%+:%> NounRoot ;

LEXICON PRN4

%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>: POSS ;
%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>%+:%> TVerbRoot ;
%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>%+:%> NounRoot ;
%<prn%>%<pers%>%<p1%>%<pl%>%<excl%>:%> CASES ;

LEXICON PRN5

%<prn%>%<pers%>%<p1%>%<pl%>%<incl%>: POSS ;
%<prn%>%<pers%>%<p1%>%<pl%>%<incl%>: # ;
%<prn%>%<pers%>%<p1%>%<pl%>%<incl%>:%> CASES ;

LEXICON PRN6

%<prn%>%<pers%>%<p1%>%<du%>%<incl%>: POSS ;
%<prn%>%<pers%>%<p1%>%<du%>%<incl%>: # ;
%<prn%>%<pers%>%<p1%>%<du%>%<incl%>:%> CASES ;

LEXICON PRN7

%<prn%>%<pers%>%<p2%>%<pl%>: POSS ;
%<prn%>%<pers%>%<p2%>%<pl%>%+:%> TVerbRoot ;
%<prn%>%<pers%>%<p2%>%<pl%>%+:%> NounRoot ;
%<prn%>%<pers%>%<p2%>%<pl%>:%> CASES ;

LEXICON PRN8

%<prn%>%<pers%>%<p3%>%<pl%>: POSS ;
%<prn%>%<pers%>%<p3%>%<pl%>%+:%> TVerbRoot ;
%<prn%>%<pers%>%<p3%>%<pl%>%+:%> NounRoot ;
%<prn%>%<pers%>%<p3%>%<pl%>:%> CASES ;

LEXICON PRN9

%<prn%>%<ref%>:и POSS ;
%<prn%>%<ref%>%+:%> TVerbRoot ;
%<prn%>%<ref%>%+:%> NounRoot ;

LEXICON IndefinitePRN

%<prn%>%<indef%>: # ;
%<prn%>%<indef%>%+:%> TVerbRoot ; ! Not sure if it can decline to other cases

LEXICON DemonstrativePRN

%<prn%>%<dem%>: # ;
%<prn%>%<dem%>%+:%> TVerbRoot ;

LEXICON InterrogativePRN
%<prn%>%<itg%>: # ;

LEXICON DET
%<det%>%<dem%>:%> NounRoot ;

LEXICON SubjAgree

%<p1%>%<sg%>:%>т CLITICS ;
%<p2%>%<sg%>:%>р CLITICS ;
%<p3%>%<sg%>:%>р CLITICS ;
%<p1%>%<pl%>:%>т CLITICS ;
%<p2%>%<pl%>:%>т CLITICS ;
%<p3%>%<pl%>:%>т CLITICS ;

LEXICON ADV1

%<adv%>:%>%{G%}у SubjAgree ;

LEXICON COMP

%<comp%>:%>ё ADV1;
ADV1 ;

LEXICON NUM1

%<num%>%<cl1%>:%>%{I%}н # ;
%<num%>%<cl2%>:%>ынь # ;
%<num%>%<cl3%>:%>аӄр # ;

LEXICON NUM2

%<num%>%<cl1%>:%>ен # ;
%<num%>%<cl2%>:%>ор # ;
%<num%>%<cl3%>:%>еӄр # ;
%<num%>%<cl4%>:%>евсӄ # ;
%<num%>%<cl5%>:%>им # ;
%<num%>%<cl6%>:%>ер # ;
%<num%>%<cl7%>:%>евор # ;
%<num%>%<cl8%>:%>ерах # ;
%<num%>%<cl9%>:%>ик # ;

LEXICON FORMALITY
%<frm%>:о CLITICS ;
%<infrm%>: CLITICS ;

LEXICON MOOD

%<itg%>:%>л FORMALITY ;
%<ind%>:%>%{д%}ь CLITICS ;
! %<imp%>:%>idk # ;

INCORPORATE-V ;

LEXICON NonFinite

%<inf%>:%>%{R%}оӽ CLITICS ;
%<cvb_nar%>: SubjAgree ;
%<cvb_ant%>:%>ӈан CLITICS ;
%<cvb_cond%>:%>ӻа CLITICS ;
MOOD ;

LEXICON TENSE

%<fut%>:%>ны NonFinite ;
NonFinite ;

LEXICON TV1
%<v%>%<tv%>: TENSE;

LEXICON IV1
%<v%>%<iv%>: TENSE;

LEXICON INCORPORATE-N
!%+:%> TVerbRoot ;
CLITICS ;

LEXICON INCORPORATE-V
!%+:%> NounRoot ;
# ;

LEXICON CnjADV
%<cnjadv%>: CLITICS;

LEXICON ADV2
%<adv%>: CLITICS;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot
умгу:%{n%}умгу N1          ; ! woman
ытык:%{n%}ытык N1          ; ! father
ӄан:%{n%}ӄан N1            ; ! doggo
ӈа:%{n%}ӈа N1              ; ! animal
тыф:%{n%}ты%{ф%} N1        ; ! house
ми:%{n%}ми N1              ; ! inside
нивх:%{n%}нивх N1          ; ! person, man
ваӄа:%{n%}ваӄа N1          ; ! box
сета:%{n%}сета N1          ; ! sugar
ойдам:%{n%}ойдам N1        ; ! baby
курӈ:%{n%}курӈ N1          ; ! universe
тяӄо:%{n%}ч%{ь%}аӄо N1     ; ! knife
оола:%{n%}оола N1          ; ! child
чʼатьф:%{n%}чʼ{ь%}атьф N1  ; ! bog, swamp
ыф:%{n%}ы%{ф%} N1          ; ! beard
арф:%{n%}ар%{ф%} N1        ; ! side of boat
ӿуньф:%{n%}ӿуньф N1        ; ! barrel
пердь:%{n%}пердь N1        ; ! burden
ыкын:%{n%}ыкын N1          ; ! older brother
атик:%{n%}атик N1          ; ! younger brother
нанак:%{n%}нанак N1        ; ! older sister
ытик:%{n%}ытик N1          ; ! grandmother
пос:%{n%}пос N1            ; ! fabric, cloth
ӈафӄ:%{n%}ӈафӄ N1          ; ! friend
леп:%{n%}леп N1            ; ! bread
ларӄ:%{n%}ларӄ N1          ; ! shirt
кинс:%{n%}кинс N1          ; ! devil
по:%{n%}по N1              ; ! village
кеӄ:%{n%}кеӄ N1            ; ! fox
чеврӄ:%{n%}ч{ь%}еврӄ N1    ; ! bird
тупр̌:%{n%}тупр̌ N1          ; ! feather
тюс:%{n%}тюс N1            ; ! meat
мулк:%{n%}мулк N1          ; ! basket
ӈояӄ:%{n%}ӈояӄ N1          ; ! egg
ӄʼас:%{n%}ӄʼас N1          ; ! tambourine
выйбукс:%{n%}выйбукс N1    ; ! tie
мифпʼидь:%{n%}мифпʼидь N1  ; ! toad
ӄʼос:%{n%}ӄʼос N1          ; ! neck
олғоӈ:%{n%}олғоӈ N1        ; ! pig
коӽ:%{n%}коӽ N1            ; ! belly
пыньх:%{n%}пыньх N1        ; ! soup
нё:%{n%}нё N1              ; ! barn

LEXICON ProperNounRoot
Рум:Рум PN1      ; ! dude called Rum

LEXICON PronounRoot
PersPronRoot ;
IndefPronRoot ;
DemPronRoot ;
IntPronRoot ;

LEXICON PersPronRoot
ни:н PRN1        ; ! I
чи:ч PRN2        ; ! you
иф:и%{f%} PRN3   ; ! he, she, it
ньыӈ:ньыӈ PRN4   ; ! they and I
мер:мер PRN5     ; ! y'all and I
меги:меги PRN6   ; ! you and I
чыӈ:чыӈ PRN7     ; ! y'all
имӈ:имӈ PRN8     ; ! they
пʼи:пʼ PRN9      ; ! oneself

LEXICON IndefPronRoot
ӿуӈрыдь:ӿуӈрыдь IndefinitePRN ; ! something like this

LEXICON DemPronRoot
тыдь:тыдь DemonstrativePRN ; ! this (one)
ӿыдь:ӿыдь DemonstrativePRN ; ! that (one)
адь:адь DemonstrativePRN   ; ! that (one)

LEXICON IntPronRoot
аӈ:аӈ InterrogativePRN ; ! who

LEXICON DeterminerRoot
ӿы:ӿы DET ; ! this

LEXICON NumeralRoot

ньин:нь NUM1 ; ! one
мен:м NUM2   ; ! two

LEXICON VerbRoot
TVerbRoot ;
IVerbRoot ;
IVerb-AdvRoot ;

LEXICON TVerbRoot
за:%{v%}з{ь%}а TV1        ; ! to beat, strike
во:%{v%}во TV1            ; ! to take
даю:%{v%}даю TV1          ; ! to write
ғе:%{v%}ғе TV1            ; ! to buy, take
ӄала:%{v%}ӄала TV1        ; ! to hate
ама:%{v%}ама TV1          ; ! to look
иғ:%{v%}иғ TV1            ; ! to kill
ху:%{v%}х%{ʼ%}у TV1       ; ! to kill
ӽа:%{v%}ӽ%{ʼ%}а TV1       ; ! to shoot
фыврк:%{v%}ф%{ʼ%}ыврк TV1 ; ! to pluck
р̌а:%{v%}р̌%{ʼ%}а TV1       ; ! to fry
фын:%{v%}ф%{ʼ%}ын TV1     ; ! to throw
хир:%{v%}х%{ʼ%}ир TV1     ; ! to use
ыӈг:%{v%}ыӈг TV1          ; ! to rip up
мот:%{v%}мот TV1          ; ! to kiss
ар:%{v%}ар TV1            ; ! to feed
ӽат:%{v%}ӽ%{ʼ%}ат TV1     ; ! to scold
амӽта:%{v%}амӽта TV1      ; ! to praise
ыз:%{v%}ыз TV1            ; ! to call
ор:%{v%}ор TV1            ; ! to meet
ӈарма:%{v%}ӈарма TV1      ; ! to wait for, await
ӽони:%{v%}ӽ%{ʼ%}они TV1   ; ! to save
ньр̌ы:%{v%}ньр̌ы TV1        ; ! to see
пʼыврк:%{v%}пʼыврк TV1    ; ! to pluck
р̌ык:%{v%}р̌ык TV1          ; ! to bring home
имы:%{v%}имы TV1          ; ! to give
лызи:%{v%}лызи TV1        ; ! to pass, go past
рғоп:%{v%}рғоп TV1        ; ! to touch

LEXICON IVerbRoot
пʼры:пʼры IV1     ; ! to come
му:му IV1         ; ! to die
чека:чека IV1     ; ! to be strong
ӿарӻо:ӿарӻо IV1   ; ! to be strong
чез:чез IV1       ; ! to be weak
чола:чола IV1     ; ! to be poor
ӈалы:ӈалы IV1     ; ! to be delirious, to talk nonsense
ӄʼо:ӄʼо IV1       ; ! to sleep
ви:ви IV1         ; ! to go
пʼу:пʼу IV1       ; ! to go out, exit
тывғ:тывғ IV1     ; ! to enter a house
тʼаӽта:тʼаӽта IV1 ; ! to be angry
ӿеӻа:ӿеӻа IV1     ; ! to be afraid
пʼолағ:пʼолағ IV1 ; ! to shout
пʼим:пʼим IV1     ; ! to run away
ӄотю:ӄотю IV1     ; ! to fall silent
оз:оз IV1         ; ! to stand up

LEXICON IVerb-AdvRoot
ур:ур COMP ; ! well
еӻ:еӻ COMP ; ! quickly

LEXICON AdverbRoot
CnjAdvRoot ;
AdvRoot ;

LEXICON CnjAdvRoot
ӿоӻор:ӿоӻор CnjADV        ; ! then, thereupon
ӿоӻорӈан:ӿоӻорӈан CnjADV  ; ! then, at that time
ӿаӈан:ӿаӈан CnjADV        ; ! then, in that case

LEXICON AdvRoot
напа:напа ADV2     ; ! still
нав:нав ADV2       ; ! now

LEXICON Punctuation
.%<sent%>:. # ;
..%<sent%>:.. # ;
...%<sent%>:... # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%'%<apos%>:%' # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
\%<sent%>:\ # ;
/%<sent%>:/ # ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst
