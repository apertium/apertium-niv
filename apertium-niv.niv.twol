Alphabet
 А Б В Г Ӷ Ғ Ӻ Д Е Ё Ж З И Й К Ӄ Л М Н Ӈ О П Р Р̌ С Т У Ф Х Ӽ Ӿ Ц Ч Ш Щ Ъ Ы Ь Э Ю Я
 а б в г ӷ ғ ӻ д е ё ж з и й к ӄ л м н ӈ о п р р̌ с т у ф х ӽ ӿ ц ч ш щ ъ ы ь э ю я ʼ
 %{a%}:а %{a%}:0 %>:0
 %{G%}:г %{G%}:к %{G%}:х %{G%}:ғ
 %{o%}:о %{o%}:0
 %{u%}:у %{u%}:0
 %{j%}:й %{j%}:0
 %{T%}:тʼ %{T%}:р̌
 %{X%}:ӽ %{X%}:ӷ
 %{y%}:ы %{y%}:0
 %{V%}:а %{V%}:э %{V%}:и %{V%}:о %{V%}:у %{V%}:ы
 %{f%}:ф %{f%}:0
 %{I%}:е %{I%}:и
 %{д%}:д %{д%}:т
 %{R%}:р %{R%}:д %{R%}:т
 %{Z%}:з %{Z%}:д %{Z%}:т %{Z%}:с
 %{ь%}:ь %{ь%}:0
 %{A%}:а %{A%}:я
 %{ф%}:ф %{ф%}:в
 %{i%}:и %{i%}:0
 %{Ч%}:т %{Ч%}:з %{Ч%}:д %{Ч%}:с
 %{Т%}:т %{Т%}:р %{Т%}:д
 %{ʼ%}:ʼ %{ʼ%}:0
 %{v%}:0
 %{n%}:0
 ;

Sets

Vowel = А Е Ё И О У Ы Э Ю Я
        а е ё и о у ы э ю я ;

IotatingVowels = А О У Э
                 а о у э ;

IotatedVowels = Я Ё Ю Е
                я ё ю е ;

Consonant = Б В Г Ӷ Ғ Ӻ Д Ж З Й К Ӄ Л М Н Ӈ П Р Р̌ С Т Ф Х Ӽ Ӿ Ц Ч Ш Щ
            б в г ӷ ғ ӻ д ж з й к ӄ л м н ӈ п р р̌ с т ф х ӽ ӿ ц ч ш щ ;

Sonorant = н ӈ м л ;

A1 = Ф Р̌ С Х Ӽ
     ф р̌ с х ӽ ;

B = П Т Ч К Ӄ
    п т ч к ӄ ;

B1 = В Р З Ғ Ӻ
     в р з ғ ӻ ;

C = Б Д Д Г Ӷ
    б д д г ӷ ;

Sibilant = З С
           з с ;

VoicelessC = К Ӄ П Р̌ С Т Ф Х Ӽ Ӿ Ц Ч Ш Щ
             к ӄ п р̌ с т ф х ӽ ӿ ц ч ш щ ;


Rules

"Remove /a/ after vowel-ending stem"
%{a%}:0 <=> :Vowel %>:0 _ ;

"Remove /u/ after vowel-ending stem"
%{u%}:0 <=> :Vowel %>:0 _ ;

"Remove /j/ after /i/"
%{j%}:0 <=> и %>:0 %{u%}:0 _ ;

"Remove /ɤ/ after vowel-ending stem"
%{y%}:0 <=> :Vowel %>:0 _ ;

"Remove /o/ after vowel-ending stem"
%{o%}:0 <=> :Vowel %>:0 %{R%}: _ ;

"Remove /f/ before consonant-initial stem"
%{f%}:0 <=> _ %>:0 %{n%}:0 :Consonant ;

"Remove /i/ before non-CC verb stems"
%{i%}:0 <=> _ \[%>:0 :Consonant :Consonant] ;

"Change iotatable vowels to iotated equivalents after /j/"
Vx:Vy <=> %{i%}: %>:0 _ ;
  where Vx in IotatingVowels
    Vy in IotatedVowels
  matched ;

"Remove soft sign archiphoneme in some places"
%{ь%}:0 <=> [ :з | :З ] _ ;
            _ :Vowel ;
            except
              [ :н | :т | :д ] _ :ы ;

"Remove real soft sign in those places"
ь:0 <=> _ (%>:0) :Vowel ;
          except
          _ (%>:0) :ы ;

"Iotate vowels after soft sign"
Vx:Vy <=> \:Sibilant [ %{ь%}: | ь: ] (%>:0) _ ;
  where Vx in IotatingVowels
    Vy in IotatedVowels
  matched ;

"Voice /f/ before sonorant and vowels"
%{ф%}:в <=> :Vowel (%>:0) _ (%>:0) [:Sonorant | :Vowel] ;

"Lengthen preceding vowel"
%{V%}:Vy <=> :Vx %>:0 _ ;
  where Vx in (а я э е и ы о ё у ю)
    Vy in (а а э э и ы о о у у)
  matched ;

"Add /a/ after consonant at morpheme boundary"
%{V%}:а <=> :Consonant %>:0 _ ;

"Set /G/ to <ғ> after vowels"
%{G%}:ғ <=> :C %>:0 _ ;

"Set /G/ to <г> after sonorants"
%{G%}:г <=> [ :Sonorant | :B1 | :Vowel ] %>:0 _ ;

"Set /G/ to <х> after voiceless stops"
%{G%}:х <=> :B %>:0 _ ;

"Set /R/ to <т> after fricatives"
%{R%}:т <=> [ :A1 | :B1 ] (%>:0) _ ;

"Set /R/ to <д> after sonorants"
%{R%}:д <=> [ :Sonorant | :Vowel ] (%>:0) _ ;
        except
          _ (:0) :A1 ;

"Change B1 to B for verbs"
Cx:Cy <=> :A1 %>:0 %{v%}:0 _ ;
          where Cx in B1
                Cy in B
          matched ;

"Change B1 to C for verbs"
Cx:Cy <=> [ :Vowel | :Sonorant ] (:0) %>:0 %{v%}:0 _ ;
          where Cx in B1
                Cy in C
          matched ;
! Sometimes doesn't change in this context

"Change B to B1 for nouns"
Cx:Cy <=> [ :й | :Sonorant ] :B1 %>:0 %{n%}:0 _ ;
            :н :ь %>:0 %{n%}:0 _ ;
            ! :ʼ %>:0 %{n%}:0 _ ;
      except
            _ :ʼ ;
            :ʼ %>:0 %{n%}:0 _ ;
            .#. _ ;
      where Cx in B
            Cy in B1
      matched ;

"<ч> to <т> where appropriate"
ч:т <=> .#. _ ;
    except
        _ [ :ы | :и | :ʼ %>:0 ] ;


"Change B to C for nouns"
Cx:Cy <=> [ :Vowel | :Sonorant ] (:0) %>:0 %{n%}:0 _ ;
      except
            _ :ʼ ;
      where Cx in B
            Cy in C
      matched ;

"Change A to A1 for nouns"
Cx:Cy <=> :Vowel [ :B | :д ] (:0) %>:0 %{n%}:0 _ ;
      where Cx in B
            Cy in A1
      matched ;

"Change A1 to A for verbs"
Cx:Cy <=> :A1 %>:0 %{v%}:0 _ ;
          :Sonorant %>:0 %{v%}:0 _ ;
      where Cx in A1
            Cy in B
      matched ;
! Sometimes doesn't change after sonorants

"Delete <ʼ> after A1"
%{ʼ%}:0 <=> :A1 (%>:0) _ ;

"Change /tʰ/ to set A1 equivalent"
%{T%}:тʼ <=> :A1 (%>:0) _ ;
! Sometimes changes after sonorants too.

"Devoice /z/ after certain aspirates for nouns"
ч:с <=> [ :ч | :п ]  :ʼ (%>:0) %{n%}:0 _ ;

"Devoice /z/ after certain aspirates for verbs"
з:с <=> [ :ч | :п ]  :ʼ (%>:0) %{v%}:0 _ ;

"Devoice /d/ after voiceless consonants"
%{д%}:т <=> :VoicelessC %>:0 _ ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst
